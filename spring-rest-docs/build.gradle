plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    //gradle 7 부터는 'org.asciidoctor.convert'이 아닌 org.asciidoctor.jvm.convert 사용
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

repositories {
    mavenCentral()
}
//2. snippets 생성 경로
ext {
    lombok_version = '1.18.24'
    snippetsDir = file('build/generated-snippets')
}

configurations {
    asciidoctorExt
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //3. mockmvc를 통해서 rest-docs 작성하기 위한 라이브러리
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    compileOnly "org.projectlombok:lombok:$lombok_version"
    annotationProcessor "org.projectlombok:lombok:$lombok_version"
    testCompileOnly "org.projectlombok:lombok:$lombok_version"
    testAnnotationProcessor "org.projectlombok:lombok:$lombok_version"
}

// test -> asciidoctor -> bootJar

// 4. 스니펫(Snippet)저장경로(build/generated-snippets)에 JunitTest 처리를 실행
test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

// 5. asciidoctor 시작할 때, static/docs 폴더 비우기
// 가끔 문서가 수정이 안되는 경우가 발생하기 때문에 선언
asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

//6. 처리된 결과를 Asciidoctor형식으로 2번에 지정해 준 스니펫(Snippet)경로에 인입
asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

// 7. build task 시 build/docs/asciidoc 파일을 src/main/resources/static/docs로 복사
task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

//8. build 시 7번 작업 진행
build {
    dependsOn copyDocument
}

//9. bootJar task 시,  build/docs/asciidoc 파일을 BOOT-INF/classes/static/docs로 복사
bootJar {
    dependsOn asciidoctor
    copy {
        from "${asciidoctor.outputDir}"
        into 'BOOT-INF/classes/static/docs'
    }

}
